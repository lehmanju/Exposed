buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'http://oss.sonatype.org/content/repositories/snapshots'
        }
        maven {
            url "http://dl.bintray.com/kotlin/kotlin"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	classpath "org.jfrog.buildinfo:build-info-extractor-gradle:+"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
    id 'net.researchgate.release' version '2.6.0'
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: "maven-publish"
apply plugin: 'com.jfrog.artifactory'

sourceCompatibility = 1.6
targetCompatibility = 1.6

defaultTasks 'jar'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        url "http://dl.bintray.com/kotlin/kotlin"
    }
    maven {
    	url "https://msrd0.duckdns.org/artifactory/gradle"
    }
}

dependencies {
    testCompile "mysql:mysql-connector-java:5.1.41"
    testCompile "mysql:mysql-connector-mxj:5.0.12"
    testCompile 'org.postgresql:postgresql:9.4.1212.jre6'
    testCompile 'ru.yandex.qatools.embed:postgresql-embedded:1.13'
    testCompile 'org.xerial:sqlite-jdbc:3.16.1'
    testCompile files('libs/ojdbc6-12.1.0.2.jar')
//    #com.hynnet:oracle-driver-ojdbc6:12.1.0.1'''
}

test {
    jvmArgs "-XX:MaxPermSize=256m"
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

publishing {
    publications {
        exposed(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifactId = 'exposed'
        }
        spring_transaction(MavenPublication) {
            from findProject(":spring-transaction").components.java
            artifact sourcesJar
            artifactId = 'spring-transaction'
        }
    }
}


bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['exposed', 'spring_transaction']
    pkg {
        userOrg = 'kotlin'
        repo = 'exposed'
        name = 'exposed'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/JetBrains/Exposed.git'
    }
}

artifactory {
  contextUrl = 'https://msrd0.duckdns.org/artifactory'
	resolve {
		repository {
			repoKey = 'gradle'
		}
	}
	publish {
		repository {
			repoKey = 'local'
			username = 'gitlab-ci'
			password = System.getenv("ARTIFACTORY_PASSWORD")
			maven = true
		}
		defaults {
			publications('exposed', 'spring_transaction')
		}
	}
}

artifactoryPublish {
	dependsOn jar
}


afterReleaseBuild.dependsOn bintrayUpload

task wrapper(type: Wrapper) {
    gradleVersion = "3.5"
}


allprojects {
    apply plugin: 'kotlin'
    apply plugin: 'maven-publish'

    group = 'org.jetbrains.exposed'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
        compile 'joda-time:joda-time:2.9.9'
        compile 'org.slf4j:slf4j-api:1.7.25'
        compileOnly 'com.h2database:h2:1.4.194'

        testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
        testCompile 'org.slf4j:slf4j-log4j12:1.7.25'
        testCompile 'log4j:log4j:1.2.17'
        testCompile 'junit:junit:4.12'
        testCompile 'org.hamcrest:hamcrest-library:1.3'
        testCompile 'com.h2database:h2:1.4.194'
    }
}


project(":spring-transaction") {
    apply plugin: 'kotlin'

    dependencies {
        compile 'org.springframework:spring-jdbc:4.3.7.RELEASE'
        compile 'org.springframework:spring-context:4.3.7.RELEASE'
        compile rootProject
        testCompile 'org.springframework:spring-test:4.3.7.RELEASE'
    }
}
